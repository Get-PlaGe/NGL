#created on: Jul 7, 2014
package birds

rule "Define variable lineParmeter for createXML specification"
	@BirdsRule(fsRule)
	dialect 'java'
	salience 300
	no-loop
    when
       $treatSpec : TreatmentSpecification(name=="createXML", project.name=="SRA")
       $job : Job()
    then
       Logger log = Logger.getLogger("ngl.sub.rules");
       log.debug("Set parameter for spec createXML");
       $job.setParameterValue("subCode",$job.getUniqueJobResource("subToXML").getProperty("code"));
end

rule "Syntax order command line for createXML specification"
	@BirdsRule(unixSyntaxRule)
	dialect 'java'
	salience 200
    when
        $strategy : UnixCommandSyntaxStrategy ( name == "createXMLSyntax" , commandLine == null)
    then
        Logger log = Logger.getLogger("ngl.sub.rules");
		log.debug("Syntax createXML : curl http://app.genoscope.cns.fr/api/submissions/codeSub");
		String parameterLine = $strategy.getParameterValue().get("subCode");
		$strategy.setCommandLine($strategy.getExecutableName()+parameterLine);
		log.debug("command line : " + $strategy.getCommandLine());

end

rule "add resources in internalRefSubmission_xml to fire sendXML"
	@BirdsRule( addResources ) 
	dialect 'java'
	salience 200
	when
		$flag : Flag ( done == false ) 
		$output : OutputSpecificationElement(treatmentSpecification.name == "createXML")
	  	$referential : ResourcesReferential ( name == "internalRefSubmissionXML" )
		$resourceSet : ResourceSet( ioSpecificationElement == $output )
		$job : Job(treatmentSpecification!=null, treatmentSpecification.name == "createXML")
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		log.debug("Add resources rules for sendXML");
		Set<ResourceProperties> setResources = new JSONDevice().parseJSONFromFileName($job.getProperty(Job.STDOUT));
		for(ResourceProperties rp : setResources){
			JobResource outputResource = $resourceSet.createJobResource($output.getResourceType(),$referential.getName());
	   		outputResource.setResourceProperties(rp);
		}
		$flag.setDone(true);
		modify($flag){};
		modify($resourceSet){};
end
