#created on: Nov 21, 2013
package rules

#declare any global variables here
declare ReadSetInfo
	laneNumber : Integer @key
	nbClusterLane : Long @key
	readSet : ReadSet @key
	validSeqPercent : Double
end

declare LaneInfo
	run : Run
	lane : Lane 
	sizeReadSets : Integer 
	readSets : List 
	nbClusterLane : Long
end

declare RunInfo
	code : String
	sizeLanes : Integer
	valuesPercentClusterFilter : List
end

rule "Initialize RunInfo"
	@nglBI( rg_1 )
	dialect "java"
	salience 600
	no-loop
	when
		$run : Run()
		not ($runInfo : RunInfo(code==$run.code))
	then
		Logger.debug("Create Run Info "+$run.getCode());
		RunInfo runInfo = new RunInfo($run.getCode(),$run.getLanes().size(),new ArrayList());
		insert(runInfo);
end

rule "Initialize LaneInfo"
	@nglBI( rg_1 )
	dialect "java"
	salience 500
	no-loop
	when
		$run : Run()
		$lane : Lane($treatment : treatments["ngsrg"], $treatment!=null) from $run.lanes
		$mapValue : Map() from $treatment.results.values()
       	$nbClusterLane : Entry(key == "nbClusterInternalAndIlluminaFilter") from $mapValue.entrySet()
       	$percentClusterFilter : Entry(key == "percentClusterIlluminaFilter") from $mapValue.entrySet()
       	$runInfo : RunInfo(code==$run.code)
	then
		Logger.debug("Create LaneInfo "+$lane.getNumber());
		LaneInfo laneInfo = new LaneInfo($run, $lane, $lane.getReadSetCodes().size(), new ArrayList(),(Long)((PropertyValue)$nbClusterLane.getValue()).getValue());
		//Add percentClusterFilter for lane to runInfo to calculate averagePercentClusterFilter
		$runInfo.getValuesPercentClusterFilter().add((PropertyValue)$percentClusterFilter.getValue());
		Logger.debug("Value percentClusterFilter : "+$percentClusterFilter.getValue());
		update($runInfo);
		insert(laneInfo);
	
end

rule "Calculate average percentClusterFilter for run"
	@nglBI( rg_1 )
	dialect "java"
	salience 450
	no-loop
	when
		$run : Run($treatment : treatments["ngsrg"], $treatment!=null)
		$mapValue : Map() from $treatment.results.values()
		$runInfo : RunInfo(sizeLanes==valuesPercentClusterFilter.size())
		$avgPercentClusterFilter : Double() 
			from accumulate ($propertyValue : PropertyValue() from $runInfo.valuesPercentClusterFilter,
			 average($propertyValue.value))
	then
		Logger.debug("Calculate average percentClusterFilter for run "+$runInfo.getCode());
		Logger.debug("Average "+$avgPercentClusterFilter);
		//Create new PropertyValue
		PropertySingleValue propertyPercentClusterFilter = new PropertySingleValue($avgPercentClusterFilter);
		$mapValue.put("percentClusterIlluminaFilter", propertyPercentClusterFilter);
		//Update treatment for Run
		MongoDBDAO.updateSet(InstanceConstants.RUN_ILLUMINA_COLL_NAME, $run, "treatments."+$treatment.getCode(), $treatment);
end 

rule "Get all readSet"
	@nglBI( rg_1 )
	dialect "java"
	salience 400
	no-loop
    when
        $run : Run( )
       	$lane : Lane( $treatment : treatments["ngsrg"], $treatment!=null) from $run.lanes
       	$readSet : String() from $lane.readSetCodes
       	$laneInfo : LaneInfo(lane==$lane);
    then
    	//Get readSet from database to calculate each validSeqPercent per readSet
    	ReadSet readSet = MongoDBDAO.findByCode(InstanceConstants.READSET_ILLUMINA_COLL_NAME, ReadSet.class, $readSet);	
    	ReadSetInfo readSetInfo = new ReadSetInfo($lane.getNumber(),$laneInfo.getNbClusterLane(),readSet);
    	//Update laneInfo
    	$laneInfo.getReadSets().add(readSetInfo);
    	insert(readSetInfo);
    	update($laneInfo);
end


rule "Calculate seqPercent readSet"
	@nglBI( rg_1 )
	dialect "java"
	salience 300
	no-loop
	when
		$readSetInfo : ReadSetInfo($treatment : readSet.treatments["ngsrg"], $treatment!=null, validSeqPercent==null)
		$mapValue : Map() from $treatment.results.values()
		$nbCluster : Entry(key=="nbCluster") from $mapValue.entrySet()
	then
		//Calculate validSeqPercent
		Long nbClusterReadSet = ((Long)((PropertyValue)$nbCluster.getValue()).getValue());
		Long nbClusterLane = $readSetInfo.getNbClusterLane();
		Double validSeqPercent = roundValue((double)nbClusterReadSet/nbClusterLane*100);
		//Create new PropertyValue
		PropertySingleValue propertyValidSeqPercent = new PropertySingleValue(validSeqPercent);
		$mapValue.put("validSeqPercent", propertyValidSeqPercent);
		//Update treatment for ReadSet in database
		MongoDBDAO.updateSet(InstanceConstants.READSET_ILLUMINA_COLL_NAME, $readSetInfo.getReadSet(), "treatments."+$treatment.getCode(), $treatment);
		$readSetInfo.setValidSeqPercent(validSeqPercent);
		update($readSetInfo);
end

rule "Calculate seqLossPercent Lane"
	@nglBI( rg_1 )
	dialect "java"
	salience 200
	no-loop
	when
		$laneInfo : LaneInfo(sizeReadSets == readSets.size(), $treatment : lane.treatments["ngsrg"], $treatment!=null)
		$mapValue : Map() from $treatment.results.values()
		$sumValidSeqPercent : Double() from accumulate (ReadSetInfo(laneNumber==$laneInfo.lane.number, validSeqPercent!=null, $value : validSeqPercent), sum($value))
	then
		Double seqLossPercent =roundValue(100-$sumValidSeqPercent);
		//Create new PropertyValue seqLossPercent
		PropertySingleValue propertySeqLossPercent = new PropertySingleValue(seqLossPercent);
		$mapValue.put("seqLossPercent", propertySeqLossPercent);
		//Update treatment for lane 
		MongoDBDAO.update(InstanceConstants.RUN_ILLUMINA_COLL_NAME, Run.class, 
						DBQuery.and(DBQuery.is("code", $laneInfo.getRun().getCode()), DBQuery.is("lanes.number", $laneInfo.getLane().getNumber())),
						DBUpdate.set("lanes.$.treatments."+$treatment.getCode(), $treatment));
end

function Double roundValue(double value)
{
	DecimalFormat df=new DecimalFormat("0.00");
	return (Double)df.parse(df.format(value)).doubleValue();
}