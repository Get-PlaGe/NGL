#created on: Aug 6, 2014
#Rules for calculations
package rules

rule "volume stock engaged in denat"
	@nglSQ( calculations )
	dialect "java"
	salience 1000
	no-loop
    when
    	 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
    	 $atomicTransfert: ManytoOneContainer( )
    	 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null, $concentrationIN:concentration) from $atomicTransfert.inputContainers
    	 $finalConcentration1 : PropertyValue() from $experimentProperties.get("finalConcentration1")
    	 $finalVolume1 : PropertyValue() from $experimentProperties.get("finalVolume1")
    	 eval($finalVolume1.value != null && $finalConcentration1.value != null && (Double)$concentrationIN.value != 0)
   	then
   		Logger.debug("Calculation Test  1: "+ $experiment.code+ " "+ $atomicTransfert.position+" "+ $finalConcentration1.value+"  "+$concentrationIN.value);
   		PropertySingleValue value = new PropertySingleValue(((Double)$finalConcentration1.value*(Double)$finalVolume1.value/(Double)$concentrationIN.value));
   		if($experimentProperties.get("requiredVolume1") != null){
   			$experimentProperties.remove("requiredVolume1");
   		}

   		$experimentProperties.put("requiredVolume1",value);
   	end
   	
rule "volume EB"
	@nglSQ( calculations )
	dialect "java"
	salience 900
	no-loop
    when
    	 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
    	 $atomicTransfert: ManytoOneContainer( )
    	 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null, $concentrationIN:concentration) from $atomicTransfert.inputContainers
    	 $naohVolume : PropertyValue() from $experimentProperties.get("NaOHVolume")
    	 $finalVolume1 : PropertyValue() from $experimentProperties.get("finalVolume1")
    	 $requiredVolume1 : PropertyValue() from $experimentProperties.get("requiredVolume1")
    	 eval($requiredVolume1.value != null && $finalVolume1.value != null && $naohVolume.value != null)
   	then
   		Logger.debug("Calculation Test  2: "+ $experiment.code+ " "+ $atomicTransfert.position+" "+$requiredVolume1.value);
   		PropertySingleValue value = new PropertySingleValue((Double)$finalVolume1.value-(Double)$requiredVolume1.value-(Double)$naohVolume.value);
   		if($experimentProperties.get("EBVolume") != null){
   			$experimentProperties.remove("EBVolume");
   		}
   		
   		$experimentProperties.put("EBVolume",value);
   	end
   	
rule "volume engaged in dilution"
	@nglSQ( calculations )
	dialect "java"
	salience 800
	no-loop
    when
    	 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
    	 $atomicTransfert: ManytoOneContainer( )
    	 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null) from $atomicTransfert.inputContainers
    	 $finalConcentration2 : PropertyValue() from $experimentProperties.get("finalConcentration2")
    	 $finalVolume2 : PropertyValue() from $experimentProperties.get("finalVolume2")
    	 $finalConcentration1 : PropertyValue() from $experimentProperties.get("finalConcentration1")
    	 eval($finalConcentration2.value != null && $finalVolume2.value != null && $finalConcentration1.value != null && (Double)$finalConcentration1.value !=0)
   	then
   		Logger.debug("Calculation Test  3: "+ $experiment.code+ " "+ $atomicTransfert.position);
   		PropertySingleValue value = new PropertySingleValue((Double)$finalConcentration2.value*(Double)$finalVolume2.value/(Double)$finalConcentration1.value);
   		if($experimentProperties.get("requiredVolume2") != null){
   			$experimentProperties.remove("requiredVolume2");
   		}
   		
   		$experimentProperties.put("requiredVolume2",value);
   	end

rule "volume HT1"
	@nglSQ( calculations )
	dialect "java"
	salience 800
	no-loop
	when
		 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
		 $atomicTransfert: ManytoOneContainer( )
		 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null) from $atomicTransfert.inputContainers
		 $phixVolume : PropertyValue() from $experimentProperties.get("phixVolume")
		 $finalVolume2 : PropertyValue() from $experimentProperties.get("finalVolume2")
		 $requiredVolume1 : PropertyValue() from $experimentProperties.get("requiredVolume1")
		 eval($phixVolume.value != null && $finalVolume2.value != null && $requiredVolume1.value != null)
	then
		Logger.debug("Calculation Test  4: "+ $experiment.code+ " "+ $atomicTransfert.position);
		PropertySingleValue value = new PropertySingleValue((Double)$finalVolume2.value-(Double)$requiredVolume1.value-(Double)$phixVolume.value);
		if($experimentProperties.get("HT1Volume") != null){
			$experimentProperties.remove("HT1Volume");
		}
		
		$experimentProperties.put("HT1Volume",value);
	end
	
rule "volume PhiX"
	@nglSQ( calculations )
	dialect "java"
	salience 800
	no-loop
	when
		 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
		 $atomicTransfert: ManytoOneContainer( )
		 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null) from $atomicTransfert.inputContainers
		 $phixPercent : PropertyValue() from $experimentProperties.get("phixPercent")
		 $finalConcentration2 : PropertyValue() from $experimentProperties.get("finalConcentration2")
		 $finalVolume2 : PropertyValue() from $experimentProperties.get("finalVolume2")
		 $phixConcentration : PropertyValue() from $experimentProperties.get("phixConcentration")
		 eval($phixPercent.value != null && $finalVolume2.value != null && $phixConcentration.value != null)
	then
		Logger.debug("Calculation Test  5: "+ $experiment.code+ " "+ $atomicTransfert.position);
		PropertySingleValue value = new PropertySingleValue((Double)$phixPercent.value*(Double)$finalConcentration2.value*(Double)$finalVolume2.value-(Double)$phixConcentration.value);
		if($experimentProperties.get("phixVolume") != null){
			$experimentProperties.remove("phixVolume");
		}
		
		$experimentProperties.put("phixVolume",value);
	end
	
rule "Volume dilution on track"
	@nglSQ( calculations )
	dialect "java"
	salience 800
	no-loop
	when
		 $experiment:Experiment($stateCode : state.code, $stateCode=="IP")
		 $atomicTransfert: ManytoOneContainer( )
		 ContainerUsed( $experimentProperties : experimentProperties,$experimentProperties!=null, $percentPerLane:percentage) from $atomicTransfert.inputContainers
		 ContainerUsed( $experimentPropertiesOut : experimentProperties,$experimentPropertiesOut!=null) from $atomicTransfert.outputContainerUsed
		 $finalVolume : PropertyValue() from $experimentPropertiesOut.get("finalVolume")
		 eval($finalVolume.value != null && $percentPerLane != null)
	then
		Logger.debug("Calculation Test  6: "+ $experiment.code+ " "+ $atomicTransfert.position);
		PropertySingleValue value = new PropertySingleValue($percentPerLane*(Double)$finalVolume.value);
		if($experimentProperties.get("requiredVolume3") != null){
			$experimentProperties.remove("requiredVolume3");
		}
		
		$experimentProperties.put("requiredVolume3",value);
	end