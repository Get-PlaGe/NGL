#created on: Aug 6, 2014
#Rules for validation
package rules

declare ContentInfo
	position : Integer
	containerCode : String 
	tag : String 
	tagCategory : String 
	sampleCode : String
end

rule "Init objects ContentInfo in WM"
		@nglSQ( validations )
	dialect "java"
	salience 600
	no-loop
    when
    $atomicTransfert: AtomicTransfertMethod($inputContainers : getInputContainers(), $inputContainers.size()!=0)
	$container : ContainerUsed( $contents : contents,$contents!=null) from $atomicTransfert.inputContainers
	$content : Content() from $contents	 
    then
   
    String tag="VIDE";
    String tagCategory="VIDE";
    if( $content.properties.containsKey("tag")) tag=$content.properties.get("tag").value.toString();
    if( $content.properties.containsKey("tagCategory")) tagCategory=$content.properties.get("tagCategory").value.toString();

	ContentInfo contentInfo= new ContentInfo($atomicTransfert.position,$container.code,tag,tagCategory,$content.sampleCode);
	Logger.debug("Content Info Position : "+$atomicTransfert.position+ ", Sample Code :"+$content.sampleCode + ", Container Code :"+$container.code+", Tag :"+tag+", tagCategory :"+tagCategory);
    
	insert(contentInfo);
	
    end

rule "Input not null for Experiment Type Prepa-flowcell"
	@nglSQ( validations )
	dialect "java"
	salience 500
	no-loop
    when
    	$experiment:Experiment($typeCode : typeCode, $typeCode=="prepa-flowcell")
    	$atomics:AtomicTransfertMethod($inputContainers : getInputContainers())
    	$contextValidation:ContextValidation()
    	eval($inputContainers.size()==0)
    then
    	//Logger.debug("Erreur position "+$atomics.position);
    	$contextValidation.addErrors("lane["+$atomics.position+"]",	"error.validationexp.emptyoutputcontainer");
    end

rule "Sum % percentage per piste = 100 % "
	@nglSQ( validations )
	dialect "java"
	salience 510
	no-loop
    when
    $atomics:AtomicTransfertMethod($inputContainers : getInputContainers())
    $total : Double() from accumulate( ContainerUsed($n:percentage, percentage!=null) from $inputContainers, sum( $n ) )
    eval($total!=100 && $total!=0)
    $contextValidation:ContextValidation()
   	then
   		//Logger.debug("Regles de validation solexa "+$total +' '+$atomics.position);
   		$contextValidation.addErrors("lane["+$atomics.position+"]",	"error.validationexp.percentperoutputcontainer", $total);
   	end
   	

   	
rule "Tag identique sur la meme position"
	@nglSQ (validations )
	dialect "java"
	salience 400
	no-loop
    when
    	   $contentInfo1 : ContentInfo($tag :tag,$sampleCode :sampleCode,$position:position,$containerCode1:containerCode)
           $contentInfo2 : ContentInfo(this != $contentInfo1, tag == $tag, position==$position, sampleCode!=$sampleCode,$containerCode2:containerCode)
           $contextValidation:ContextValidation()
    then
    	//Logger.debug("Tag error "+$contentInfo1 + " position "+$position);
    	$contextValidation.addErrors("lane["+$position+"]",	"error.validationexp.sametag", $tag,$containerCode1,$containerCode2);    	
    end

rule "Many TagCategory on same position"
	@nglSQ (validations )
	dialect "java"
	salience 300
	no-loop
when
	 ContentInfo($position:position)
     $tagCategories : Set(empty==false, size > 1) from accumulate( ContentInfo ( $tagCategory : tagCategory,$position==position),
                        collectSet( $tagCategory) )
	  $contextValidation:ContextValidation($errors : errors)
//	  not ($entry: Map.Entry(empty==true) from $errors.entrySet() 
//	   ValidationError(key=="",message=="") from $entry.getValue() )
then
	Logger.debug("TagCategory error "+ $tagCategories.size());
    $contextValidation.addErrors("lane["+$position+"]",	"error.validationexp.manytagCategory", "");
end