//Rules for validation
package rules

declare SourceInfo
	value : String
	supportCodes : Set 	
end

declare SourceSupportInfo
	supportCode : String
	sources : Set 	
end

// 01/02/2017 janus-and-cBot devient janus-and-cBotV2 
rule "Init SourceInfo for janus+cbot v2"
    @nglSQ( validations )
	dialect "java"
	salience 1000
	no-loop
    when
    	$experiment:Experiment($typeCode:typeCode,$typeCode=="prepa-fc-ordered", instrument.typeCode == "janus-and-cBotV2")    	
    then
    	Logger.debug("Init SourceInfo for janus+cbotV2");
    	SourceInfo sourceInfo1 = new SourceInfo("1",new TreeSet());
	    insert(sourceInfo1);
	    SourceInfo sourceInfo2 = new SourceInfo("2",new TreeSet());
	    insert(sourceInfo2);
	    SourceInfo sourceInfo3 = new SourceInfo("3",new TreeSet());
	    insert(sourceInfo3);
	    SourceInfo sourceInfo4 = new SourceInfo("4",new TreeSet());
	    insert(sourceInfo4);
end

// 01/02/2017 janus-and-cBot devient janus-and-cBotV2 
rule "Init SourceSupportInfo for janus+cbot v2"
    @nglSQ( validations )
	dialect "java"
	salience 1000
	no-loop
    when
    	$experiment:Experiment($typeCode:typeCode,$typeCode=="prepa-fc-ordered", instrument.typeCode == "janus-and-cBotV2") 
    	$atomicTransfert: ManyToOneContainer($inputContainers : inputContainerUseds, $inputContainers.size()!=0)
    	$container: InputContainerUsed($source:instrumentProperties.source.value) from $inputContainers
    	not SourceSupportInfo($container.locationOnContainerSupport.code==supportCode)   	
    then
    	Logger.debug("Init SourceSupportInfo for janus+cbot v2");
    	SourceSupportInfo sourceSupportInfo = new SourceSupportInfo($container.locationOnContainerSupport.code,new TreeSet());
    	insert(sourceSupportInfo);
end

// 01/02/2017 janus-and-cBot devient janus-and-cBotV2 
rule "add supportcode for source"
    @nglSQ( validations )
	dialect "java"
	salience 900
	no-loop
    when
    	$experiment:Experiment($typeCode:typeCode,$typeCode=="prepa-fc-ordered", instrument.typeCode == "janus-and-cBotV2")
    	$atomicTransfert: ManyToOneContainer($inputContainers : inputContainerUseds, $inputContainers.size()!=0)
    	$container: InputContainerUsed($source:instrumentProperties.source.value) from $inputContainers
    	$sourceInfo : SourceInfo($source==value)
    then
    	//Logger.debug("find sourceInfo for "+$source+" "+$sourceInfo.getSupportCodes().size());
    	$sourceInfo.getSupportCodes().add($container.locationOnContainerSupport.code);
    	modify($sourceInfo){};
end

// 01/02/2017 janus-and-cBot devient janus-and-cBotV2 
rule "add source for supportcode"
    @nglSQ( validations )
	dialect "java"
	salience 900
	no-loop
    when
    	$experiment:Experiment($typeCode:typeCode,$typeCode=="prepa-fc-ordered", instrument.typeCode == "janus-and-cBotV2")
    	$atomicTransfert: ManyToOneContainer($inputContainers : inputContainerUseds, $inputContainers.size()!=0)
    	$container: InputContainerUsed($source:instrumentProperties.source.value) from $inputContainers
    	$sourceSupportInfo : SourceSupportInfo($container.locationOnContainerSupport.code==supportCode)
    then
    	Logger.debug("find sourceSupportInfo for "+$container.locationOnContainerSupport.code+" "+$sourceSupportInfo.getSources().size());
    	$sourceSupportInfo.getSources().add($source);
    	modify($sourceSupportInfo){};
end

rule "check nb support for one source"
    @nglSQ( validations )
	dialect "java"
	salience 700
	no-loop
    when
    	$sourceInfo : SourceInfo($source:value, supportCodes.size() > 1 )
    	$contextValidation:ContextValidation()    		
    then
    	$contextValidation.addErrors("source",	"plusieurs supports "+$sourceInfo.getSupportCodes()+" ont la meme source : "+$source);	
end

rule "check nb source for one support"
    @nglSQ( validations )
	dialect "java"
	salience 700
	no-loop
    when
    	$sourceSupportInfo : SourceSupportInfo($supportCode : supportCode, sources.size() > 1)
    	$contextValidation:ContextValidation()    		
    then
    	$contextValidation.addErrors("source",	"plusieurs sources "+$sourceSupportInfo.getSources()+" pour un meme support : "+$supportCode);    		
end


//  01/02/2017 NGL-1141: Les proprietes stripCode et cbotFile des CbotV2 ne peuvent pas etre rendues obligatoires dans tous les cas...
//  - pas mis dans obligatoire dans instruments => rendre obligatoire ici
//  - ATTENTION les regles se declenchent pour toute sauvegarde.Or on veut que la regle ne se declenche que pour le passage a l'etat terminÃ© (F)
//  - plus simple de mettre le code de controle dans la partie when...
//  - Drools ne fait pas le typage automatique comme java1.8=> cast explicite necessaire

rule "check cbotFile and stripCode"
    @nglSQ( validations )
	dialect "java"
	salience 1000
	no-loop
    when
    	State (code == 'F')
    	Experiment(typeCode =="prepa-fc-ordered", instrument.typeCode == "janus-and-cBotV2", $instrumentProperties:instrumentProperties)
    	$contextValidation:ContextValidation() 
    then
        /*
    	if ($instrumentProperties.containsKey("stripCode")){
    		PropertySingleValue stripCode = (PropertySingleValue) $instrumentProperties.get("stripCode");
    		if(StringUtils.isBlank((String)stripCode.value)){
    		
    			$contextValidation.addErrors("instrumentProperties.stripCode", "error.required"); 
    		}
    	}else{
    		$contextValidation.addErrors("instrumentProperties.stripCode", "error.required");
    	}
    	*/
    	
    	if ($instrumentProperties.containsKey("cbotFile")){
    		PropertySingleValue cbotFile = (PropertySingleValue) $instrumentProperties.get("cbotFile");
    		if (StringUtils.isBlank((String)cbotFile.value)){

    			$contextValidation.addErrors("instrumentProperties.cbotFile", "error.required"); 
    		}
    	}else{
    		$contextValidation.addErrors("instrumentProperties.cbotFile", "error.required");
    	}
    	
end        